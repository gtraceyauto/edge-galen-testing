@import galen-extras/galen-extras-rules.gspec

@objects
  widget_container             #shs-football-league-standings
  header                       .widget-header
  toggle_container             .toggle-container
      tab-*                    .toggle
  afc_conference                #shs-football-league-standings div.conference-container:nth-child(2)
      name                     .conference-name
      division-*               .division-container
  nfc_conference                #shs-football-league-standings div.conference-container:nth-child(3)
      name                     .conference-name
      division-*               .division-container
  division_header              .division-header
      name                     .division-name
      category_desktop-*       .stat-header:not(.show-for-medium-only)
      category_tablet-*        .stat-header:not(.show-for-large)
      category_mobile-*        .stat-header:not(.hide-for-small-only):not(.show-for-large):not(.show-for-medium-only)
  team_logo-*                  .team-logo
  team_row-*                   .team-container
  full_team_name-*             .full-team-name
  team_abbreviation-*          .team-abbreviation
  stat_wins-*                  .wins
  stat_losses-*                .losses
  stat_ties-*                  .ties
  stat_win_percent-*           .win-percentage
  stat_home_record-*           .home
  stat_away_record-*           .away
  stat_div_record-*            .division
  stat_conf_record-*           .conference
  stat_points_for-*            .points-for
  stat_points_against-*        .points-against
  stat_diff-*                  .differential
  stat_streak-*                .streak

@groups
  conference_names     afc_conference.name, nfc_conference.name

@script
  stats_header_desktop = ["W", "L", "T", "PCT", "HOME", "AWAY", "DIV", "CONF", "PF", "PA", "DIFF", "STRK"];
  stats_header_tablet = ["W", "L", "T", "PCT", "HOME", "AWAY", "DIV", "CONF", "PF", "PA", "DIFF", "STRK"];
  stats_header_mobile = ["W", "L", "T", "PCT", "DIV", "STRK"];

= Header =
  @on desktop, tablet
    header:
      height ~40 px
      text uppercase is "NFL STANDINGS"

  @on mobile
    header:
      height ~24 px
      text uppercase is "NFL STANDINGS"

= Conferences =
  @on *
    afc_conference:
      above nfc_conference
      aligned vertically all nfc_conference

    afc_conference.name:
      text uppercase is "AFC"

    nfc_conference.name:
      text uppercase is "NFC"
      aligned vertically all afc_conference.name

  @on desktop, tablet
    | every &conference_names is inside widget_container 0 px left

  @on mobile
    | every &conference_names is inside widget_container 0 px left

= Divisions =
  @on *
    | amount of visible afc_conference.division-* should be 4
    | amount of visible nfc_conference.division-* should be 4
    | afc_conference.division-* are aligned vertically above each other with 32 px margin
    | nfc_conference.division-* are aligned vertically above each other with 32 px margin

= Teams =
  @on *
    | amount of visible team_row-* should be 32
    | every team_logo-* has height 24px and has width 24px

    @forEach [team_logo-*] as teamLogo, index as i
      ${teamLogo}:
        centered vertically inside team_row-${i}

  @on desktop, tablet
    @for [1 - 29] as i
      full_team_name-${i}:
        aligned vertically left full_team_name-${i + 1}
        centered vertically inside team_row-${i}

      team_logo-${i}:
        aligned vertically all team_logo-${i + 1}

  @on mobile
    @for [1 - 29] as i
      team_abbreviation-${i}:
        aligned vertically left team_abbreviation-${i + 1}
        centered vertically inside team_row-${i}

      team_logo-${i}:
        aligned vertically all team_logo-${i + 1}

= Stats Table Header =
  @on desktop
    @forEach [division_header.category_desktop-*] as category, index as i
      ${category}:
        text uppercase is "${stats_header_desktop[i-1]}"
        aligned horizontally all division_header.name

  @on tablet
    @forEach [division_header.category_tablet-*] as category, index as i
      ${category}:
        text uppercase is "${stats_header_tablet[i-1]}"
        aligned horizontally all division_header.name

  @on mobile
    @forEach [division_header.category_mobile-*] as category, index as i
      ${category}:
        text uppercase is "${stats_header_mobile[i-1]}"
        aligned horizontally all division_header.name

= Stats Table =
  @on *
    @for [1 - 39] as i
      stat_wins-${i}:
        aligned vertically centered stat_wins-${i + 1} 1px

    @for [1 - 39] as i
      stat_losses-${i}:
        aligned vertically centered stat_losses-${i + 1} 1px

    @for [1 - 39] as i
      stat_ties-${i}:
        aligned vertically centered stat_ties-${i + 1} 1px

    @for [1 - 39] as i
      stat_win_percent-${i}:
        aligned vertically centered stat_win_percent-${i + 1} 1px

    @for [1 - 39] as i
      stat_div_record-${i}:
        aligned vertically centered stat_div_record-${i + 1} 1px

    @for [1 - 39] as i
      stat_streak-${i}:
        aligned vertically centered stat_streak-${i + 1} 1px

  @on desktop, tablet
    @for [1 - 39] as i
      stat_home_record-${i}:
        aligned vertically centered stat_home_record-${i + 1} 1px

    @for [1 - 39] as i
      stat_away_record-${i}:
        aligned vertically centered stat_away_record-${i + 1} 1px

    @for [1 - 39] as i
      stat_conf_record-${i}:
        aligned vertically centered stat_conf_record-${i + 1} 1px

    @for [1 - 39] as i
      stat_points_for-${i}:
        aligned vertically centered stat_points_for-${i + 1} 1px

    @for [1 - 39] as i
      stat_points_against-${i}:
        aligned vertically centered stat_points_against-${i + 1} 1px

    @for [1 - 39] as i
      stat_diff-${i}:
        aligned vertically centered stat_diff-${i + 1} 1px

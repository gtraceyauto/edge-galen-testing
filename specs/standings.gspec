@import galen-extras/galen-extras-rules.gspec

@objects
  widget_container             #mlb-league-standings
  header                       .widget-header
  toggle_container             .toggle-container
      tab-*                    .toggle
  al_conference                #mlb-league-standings div.conference-container:nth-child(3)
      name                     .conference-name
      division-*               .division-container
  nl_conference                #mlb-league-standings div.conference-container:nth-child(4)
      name                     .conference-name
      division-*               .division-container
  division_header              .division-header
      name_large               .division-name.hide-for-small-only
      name_small               .division-name.show-for-small-only
      category_desktop-*       .stat-header:not(.show-for-medium-only)
      category_tablet-*        .stat-header:not(.show-for-large)
      category_mobile-*        .stat-header:not(.hide-for-small-only):not(.show-for-large):not(.show-for-medium-only)
  team_logo-*                  .team-logo
  team_row-*                   .team-container
  full_team_name-*             .full-team-name
  team_abbreviation-*          .team-abbreviation
  stat_wins-*                  .wins
  stat_losses-*                .losses
  stat_win_percent-*           .win-percentage
  stat_games_behind-*          .games-behind
  stat_last_ten-*              .last-ten
  stat_streak-*                .streak
  stat_home_record-*           .home-record
  stat_away_record-*           .away-record
  stat_east_record-*           .east-record
  stat_central_record-*        .central-record:not(.show-for-medium-only)
  stat_central_record_m-*      .central-record:not(.show-for-large)
  stat_west_record-*           .west-record
  stat_runs_scored-*           .runs-scored
  stat_runs_allowed-*          .runs-allowed
  stat_runs_diff-*             .run-differential

@groups
  conference_names     al_conference.name, nl_conference.name

@script
  stats_header_desktop = ["W", "L", "PCT", "GB", "L10", "STRK", "HOME", "AWAY", "EAST", "CENTRAL", "WEST", "RS", "RA", "DIFF"];
  stats_header_tablet = ["W", "L", "PCT", "GB", "L10", "STRK", "HOME", "AWAY", "EAST", "CENT", "WEST"];
  stats_header_mobile = ["W", "L", "PCT", "GB", "L10", "STRK"];

= Header =
  @on desktop, tablet
    header:
      height 40 px
      text uppercase is "MLB STANDINGS"

  @on mobile
    header:
      height 24 px
      text uppercase is "MLB STANDINGS"

= Tabs =
  @on *
    toggle_container:
      centered horizontally inside widget_container

    toggle_container.tab-1:
      below header ~22 px
      text uppercase is "DIVISION"

    toggle_container.tab-2:
      below header ~22 px
      text uppercase is "WILDCARD"

    toggle_container.tab-3:
      below header ~22 px
      text uppercase is "LEAGUE"

    | toggle_container.tab-* are aligned horizontally next to each other

= Conferences =
  @on *
    al_conference:
      above nl_conference
      aligned vertically all nl_conference

    al_conference.name:
      text uppercase is "AMERICAN LEAGUE"
      below toggle_container.tab-1 ~22 px

    nl_conference.name:
      text uppercase is "NATIONAL LEAGUE"
      aligned vertically all al_conference.name

  @on desktop, tablet
    | every &conference_names is inside widget_container 24 px left

  @on mobile
    | every &conference_names is inside widget_container 16 px left

= Divisions =
  @on *
    | amount of visible al_conference.division-* should be 3
    | amount of visible nl_conference.division-* should be 3
    | al_conference.division-* are aligned vertically above each other with 32 px margin
    | nl_conference.division-* are aligned vertically above each other with 32 px margin

= Teams =
  @on *
    | amount of visible team_row-* should be 30
    | every team_logo-* has height 24px and has width 24px

    @forEach [team_logo-*] as teamLogo, index as i
      ${teamLogo}:
        centered vertically inside team_row-${i}

  @on desktop, tablet
    @for [1 - 29] as i
      full_team_name-${i}:
        aligned vertically left full_team_name-${i + 1}
        centered vertically inside team_row-${i}

      team_logo-${i}:
        aligned vertically all team_logo-${i + 1}

  @on mobile
    @for [1 - 29] as i
      team_abbreviation-${i}:
        aligned vertically left team_abbreviation-${i + 1}
        centered vertically inside team_row-${i}

      team_logo-${i}:
        aligned vertically all team_logo-${i + 1}

= Stats Table Header =
  @on desktop
    @forEach [division_header.category_desktop-*] as category, index as i
      ${category}:
        text uppercase is "${stats_header_desktop[i-1]}"
        aligned horizontally all division_header.name_large

  @on tablet
    @forEach [division_header.category_tablet-*] as category, index as i
      ${category}:
        text uppercase is "${stats_header_tablet[i-1]}"
        aligned horizontally all division_header.name_large

  @on mobile
    @forEach [division_header.category_mobile-*] as category, index as i
      ${category}:
        text uppercase is "${stats_header_mobile[i-1]}"
        aligned horizontally all division_header.name_small

= Stats Table =
  @on *
    @for [1 - 35] as i
      stat_wins-${i}:
        aligned vertically centered stat_wins-${i + 1} 1px

    @for [1 - 35] as i
      stat_losses-${i}:
        aligned vertically centered stat_losses-${i + 1} 1px

    @for [1 - 35] as i
      stat_win_percent-${i}:
        aligned vertically centered stat_win_percent-${i + 1} 1px

    @for [1 - 35] as i
      stat_games_behind-${i}:
        aligned vertically centered stat_games_behind-${i + 1} 1px

    @for [1 - 35] as i
      stat_last_ten-${i}:
        aligned vertically centered stat_last_ten-${i + 1} 1px

    @for [1 - 35] as i
      stat_streak-${i}:
        aligned vertically centered stat_streak-${i + 1} 1px

  @on desktop, tablet
    @for [1 - 35] as i
      stat_home_record-${i}:
        aligned vertically centered stat_home_record-${i + 1} 1px

    @for [1 - 35] as i
      stat_away_record-${i}:
        aligned vertically centered stat_away_record-${i + 1} 1px

    @for [1 - 35] as i
      stat_east_record-${i}:
        aligned vertically centered stat_east_record-${i + 1} 1px

    @for [1 - 35] as i
      stat_west_record-${i}:
        aligned vertically centered stat_west_record-${i + 1} 1px

  @on desktop
    @for [1 - 35] as i
      stat_central_record-${i}:
        aligned vertically centered stat_central_record-${i + 1} 1px

    @for [1 - 35] as i
      stat_runs_scored-${i}:
        aligned vertically centered stat_runs_scored-${i + 1} 1px

    @for [1 - 35] as i
      stat_runs_allowed-${i}:
        aligned vertically centered stat_runs_allowed-${i + 1} 1px

    @for [1 - 35] as i
      stat_runs_diff-${i}:
        aligned vertically centered stat_runs_diff-${i + 1} 1px

  @on tablet
    @for [1 - 35] as i
      stat_central_record_m-${i}:
        aligned vertically centered stat_central_record_m-${i + 1} 1px
